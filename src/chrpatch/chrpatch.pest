// Primitives
num_silent = _{ ASCII_DIGIT+ }
num = { num_silent }
rest = { (!"\n" ~ ANY)+ }

// Patch content
action_rename = { "rename " ~ path }
action_replace = { "replace " ~ num ~ " with " ~ rest }
action_push = { "push " ~ num ~ "+" ~ num ~ " " ~ rest }
action_delete = { "delete " ~ num }
statement = { action_delete | action_push | action_rename | action_replace }

// Meta
separator = { "=========>" }

// Header contents
author = { "" }
date = { num_silent }
type = { "create" | "update" | "delete" }
path = { !("/" | "-") ~ pathspec+ }
pathspec = _{ "/"* ~ (ASCII_ALPHANUMERIC | "_" | "-" | ".")+ }

// Global
header = { "type: " ~ type ~ "\n" ~ "author: " ~ author ~ "\n" ~ "date: " ~ date ~ "\n" ~ "description: " ~ rest ~ "\n" }
patch = { separator ~ ("\n" ~ statement)+ ~ "\n" ~ separator }
file = { SOI ~ header ~ (patch ~ "\n")+ ~ EOI }
