//! CHRPatch Parsing Expression Grammar

// Primitives
num_silent = _{ ASCII_DIGIT+ }
number = { num_silent }  // Alias to eliminate ambiguity
line = { num_silent } // Alias to eliminate ambiguity
offset = { num_silent } // Alias to eliminate ambiguity
rest = { (!NEWLINE ~ ANY)+ }

// Patch content
type = @{ "create" | "update" | "delete" }
path = { !("/" | "-") ~ (!(NEWLINE | " >") ~ pathspec)+ }
pathspec = _{ "/"* ~ (ASCII_ALPHANUMERIC | "_" | "-" | ".")+ }
patch_header = _{ "= " ~ type ~ ": " ~ path ~ " >" }
action_move = { "move " ~ path }
action_replace = { "replace " ~ line ~ " " ~ rest }
action_push = { "push " ~ line ~ "+" ~ offset ~ " " ~ rest }
action_cut = { "cut " ~ number }
statement = _{ action_cut | action_push | action_move | action_replace }
statements = { (NEWLINE ~ statement)+ }

// Header contents
username = { (!" [" ~ (ASCII_ALPHANUMERIC | "_" | "-"))+ }
who = { (!"@" ~ ASCII_ALPHANUMERIC)+ }
at = { (!"]" ~ (ASCII_ALPHANUMERIC | "." | "_"))+ }
email = { who ~ "@" ~ at }
author = { username ~ " [" ~ email ~ "]" }
date = { num_silent }

// Global
header = {
    "author: " ~ author ~ NEWLINE
    ~ "date: " ~ date ~ NEWLINE
    ~ "description: " ~ rest ~ NEWLINE
}
patch = { patch_header ~ statements ~ NEWLINE ~ patch_header }
file = { SOI ~ header ~ NEWLINE ~ (patch ~ NEWLINE*)+ ~ EOI }
