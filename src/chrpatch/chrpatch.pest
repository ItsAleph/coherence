//! CHRPatch Parsing Expression Grammar

// Primitives
num_silent = _{ ASCII_DIGIT+ }
number = { num_silent }  // Alias to eliminate ambiguity
line = { num_silent } // Alias to eliminate ambiguity
offset = { num_silent } // Alias to eliminate ambiguity
rest = { (!"\n" ~ ANY)+ }

// Patch content
type = { "create" | "update" | "delete" }
path = { !("/" | "-") ~ (!("\n" | " >") ~ pathspec)+ }
pathspec = _{ "/"* ~ (ASCII_ALPHANUMERIC | "_" | "-" | ".")+ }
patch_header = { "= " ~ type ~ ": " ~ path ~ " >" }
action_move = { "move " ~ path }
action_replace = { "replace " ~ line ~ rest }
action_push = { "push " ~ line ~ "+" ~ offset ~ " " ~ rest }
action_cut = { "cut " ~ number }
statement = { action_cut | action_push | action_move | action_replace }

// Header contents
username = { (!" [" ~ (ASCII_ALPHANUMERIC | "_" | "-"))+ }
who = { (!"@" ~ ASCII_ALPHANUMERIC)+ }
at = { (!"]" ~ (ASCII_ALPHANUMERIC | "." | "_"))+ }
email = { who ~ "@" ~ at }
author = { username ~ " [" ~ email ~ "]" }
date = { num_silent }

// Global
header = {
    "author: " ~ author ~ "\n"
    ~ "date: " ~ date ~ "\n"
    ~ "description: " ~ rest ~ "\n"
}
patch = { patch_header ~ ("\n" ~ statement)+ ~ "\n" ~ patch_header }
file = { SOI ~ header ~ "\n" ~ (patch ~ "\n")+ ~ "\n"* ~ EOI }
