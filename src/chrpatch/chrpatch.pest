// Primitives
num_silent = _{ ASCII_DIGIT+ }
num = { num_silent }
rest = _{ (!"\n" ~ ANY)+ }

// Patch content
action_rename = { "rename " ~ path }
action_replace = { "replace " ~ num ~ rest }
action_push = { "push " ~ num ~ "+" ~ num ~ " " ~ rest }
action_delete = { "delete " ~ num }
statement = { action_delete | action_push | action_rename | action_replace }

// Meta
separator = { "=========>" }

// Header contents
username = { (!" [" ~ (ASCII_ALPHANUMERIC | "_" | "-"))+ }
who = { (!"@" ~ ASCII_ALPHANUMERIC)+ }
at = { (!"]" ~ (ASCII_ALPHANUMERIC | "." | "_"))+ }
email = { who ~ "@" ~ at }
author = { username ~ " [" ~ email ~ "]" }
date = { num_silent }
type = { "create" | "update" | "delete" }
path = { !("/" | "-") ~ (!"\n" ~ pathspec)+ }
pathspec = _{ "/"* ~ (ASCII_ALPHANUMERIC | "_" | "-" | ".")+ }

// Global
header = { "type: " ~ type ~ "\n" ~ "path: " ~ path ~ "\n" ~ "author: " ~ author ~ "\n" ~ "date: " ~ date ~ "\n" ~ "description: " ~ rest ~ "\n" }
patch = { separator ~ ("\n" ~ statement)+ ~ "\n" ~ separator }
file = _{ SOI ~ header ~ "\n" ~ (patch ~ "\n")+ ~ EOI }
